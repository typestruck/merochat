module Client.Profile.Update where

import Prelude

import Client.AppId (imAppId)
import Client.Dom as CCD
import Client.File as CCF
import Client.Network (routes)
import Client.Network as CN
import Data.Array ((:))
import Data.Array as DA
import Data.Either (Either(..))
import Data.Int as DI
import Data.Maybe (Maybe(..))
import Data.Maybe as DM
import Data.String as DS
import Data.String.Read as DSR
import Data.Symbol (class IsSymbol)
import Data.Tuple as DT
import Data.Tuple.Nested (type (/\), (/\))
import Debug (spy)
import Effect (Effect)
import Effect.Aff (Aff)
import Effect.Class (liftEffect)
import Effect.Class as EC
import Flame (Update)
import Flame.Subscription as FS
import Payload.ResponseTypes (Response(..))
import Prim.Row (class Cons)
import Record as R
import Shared.Ask (Ask)
import Shared.DateTime (DateWrapper(..))
import Shared.DateTime as SDT
import Shared.Element (ElementId(..))
import Shared.Im.Types (RetryableRequest(..))
import Shared.Im.Types as SIT
import Shared.Modal (ScreenModal(..))
import Shared.Network (RequestStatus(..))
import Shared.Network as SN
import Shared.Options.Profile (maxFinalTags, maxLanguages, maxStartingTags)
import Shared.Privilege (Privilege(..))
import Shared.Profile.Types (PM, ProfileAsk, ProfileMessage(..), ProfileMode(..), ProfileModel, What(..))
import Shared.ProfileColumn as SP
import Shared.ResizeInput as SIR
import Type.Proxy (Proxy(..))
import Web.DOM (Element)

getFileInput ∷ Effect Element
getFileInput = CCD.unsafeGetElementById AvatarFileInput

update ∷ Update ProfileModel ProfileMessage
update model = case _ of
      SetPField upd → setField model upd
      SelectAvatar → selectAvatar model
      RefreshPosts → refreshPosts model
      SetCountry value → setCountry value model
      SetAge value → setAge value model
      SetGender value → setGender value model
      Clear → clear model
      ToggleVisibility modal → setVisibility model modal
      SetAutoGenerated what event → setAutoGenerated model what event
      SetLanguage value → setLanguage value model
      SetTag value → setTag value model
      Save → save model
      ResizeChatInput event → SIR.resizeInputFrom event model
      AfterRegistration → setFromTemporary model
      UpdatePrivileges _ → model /\ []
      RefreshAsks → refreshAsks model
      SetAnswer id value → setAnswer id value model
      SendAnswer id → sendAnswer id model
      AfterSendAnswer id → afterSendAnswer id model
      ToggleAskMenu id → toggleAskMenu id model
      IgnoreAsk id → ignoreAsk id model

ignoreAsk ∷ Int → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
ignoreAsk id model = model { asks = DA.filter ((_ /= id) <<< _.id) model.asks } /\ [ ignore ]
      where
      ignore = do
            void <<< CN.silentRequest $ routes.profile.ignore { body: { id } }
            pure Nothing

toggleAskMenu ∷ Maybe Int → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
toggleAskMenu id model = model { contextMenuFor = id } /\ []

afterSendAnswer ∷ Int → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
afterSendAnswer id model = model { loading = false, asks = map upd model.asks } /\ []
      where
      upd ask
            | ask.id == id = ask { answer = ask.typedAnswer }
            | otherwise = ask

sendAnswer ∷ Int → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
sendAnswer id model = model { loading = DM.isJust answer } /\ effects
      where
      answer = DA.find ((_ == id) <<< _.id) model.asks >>= _.typedAnswer

      send value = do
            void <<< CN.silentRequest $ routes.profile.answer { body: { id, answer: value } }
            pure <<< Just $ AfterSendAnswer id
      effects = case answer of
            Just value → [ send value ]
            Nothing → []

setAnswer ∷ Int → Maybe String → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setAnswer id value model = model { asks = map upd model.asks } /\ []
      where
      upd ask
            | ask.id == id = ask { typedAnswer = value }
            | otherwise = ask

refreshAsks ∷ ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
refreshAsks model = model { mode = Asked } /\ [ fetch ]
      where
      fetch = do
            result ← routes.profile.asks { query: { after: _.id <$> DA.head model.asks } }
            case result of
                  Right (Response response) → pure <<< Just <<< SetPField $ _ { asks = map extend response.body <> model.asks }
                  _ → pure Nothing
      extend ask = (R.merge (ask ∷ Ask) { typedAnswer: Nothing ∷ Maybe String }) ∷ ProfileAsk

refreshPosts ∷ ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
refreshPosts model = model { mode = OwnPosts } /\ [ fetch ]
      where
      fetch = do
            result ← routes.profile.posts { query: { after: _.id <$> DA.head model.posts } }
            case result of
                  Right (Response response) → pure <<< Just <<< SetPField $ _ { posts = response.body <> model.posts }
                  _ → pure Nothing

clear ∷ ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
clear model =
      model
            { nameInputed = Just model.user.name
            , headlineInputed = Just model.user.headline
            , descriptionInputed = Just model.user.description
            , ageInputed = model.user.age
            , fromTemporary = false
            , updateRequestStatus = Nothing
            , tagsInputed = model.user.tags
            , countryInputed = model.user.country
            , languagesInputed = model.user.languages
            , genderInputed = model.user.gender
            , avatarInputed = model.user.avatar
            } /\ []

save ∷ ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
save model = model { loading = true, fromTemporary = false } /\ [ saveIt ]
      where
      fields =
            { name: { value: DM.fromMaybe model.user.name model.nameInputed, generated: DA.elem Name model.generated }
            , headline: { value: DM.fromMaybe model.user.headline model.headlineInputed, generated: DA.elem Headline model.generated }
            , description: { value: DM.fromMaybe model.user.description model.descriptionInputed, generated: DA.elem Description model.generated }
            , age: model.ageInputed
            , tags: model.tagsInputed
            , country: model.countryInputed
            , languages: model.languagesInputed
            , gender: model.genderInputed
            , avatar: model.avatarInputed
            }
      saveIt = do
            result ← routes.profile.save { body: fields }
            case result of
                  Right (Response response) → do
                        EC.liftEffect do
                              FS.send imAppId $ SIT.SetNameFromProfile fields.name.value
                              FS.send imAppId $ SIT.SetAvatarFromProfile response.body.avatar
                              FS.send imAppId $ SIT.SetCompletedFields
                                    ( (if DA.elem Name model.generated || model.nameInputed == Just model.user.name then [ SP.Name ] else [])
                                            <>
                                                  (if DA.elem Headline model.generated || model.headlineInputed == Just model.user.headline then [] else [ SP.Headline ])
                                            <>
                                                  (if DA.elem Description model.generated || model.descriptionInputed == Just model.user.description then [] else [ SP.Description ])
                                            <>
                                                  (if DM.isNothing model.ageInputed then [] else [ SP.Birthday ])
                                            <>
                                                  (if DA.null model.tagsInputed then [] else [ SP.Tags ])
                                            <>
                                                  (if DM.isNothing model.countryInputed then [] else [ SP.Country ])
                                            <>
                                                  (if DA.null model.languagesInputed then [] else [ SP.Languages ])
                                            <>
                                                  (if DM.isNothing model.genderInputed then [] else [ SP.Gender ])
                                            <>
                                                  (if DM.isNothing model.avatarInputed then [] else [ SP.Avatar ])
                                    )
                        pure <<< Just <<< SetPField $ _ { loading = false }
                  Left err → do
                        pure <<< Just <<< SetPField $ _ { updateRequestStatus = Just <<< Failure $ SN.errorMessage err }

setFromTemporary ∷ ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setFromTemporary model = model { fromTemporary = true } /\ []

setAge ∷ String → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setAge value model = model { ageInputed = DateWrapper <$> SDT.unformatIsoDate value } /\ []

setCountry ∷ String → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setCountry value model = model { countryInputed = DI.fromString value } /\ []

setGender ∷ String → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setGender value model = model { genderInputed = DSR.read value } /\ []

setTag ∷ String → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setTag tag model =
      model
            { tagsInputed =
                    if DA.elem tag model.tagsInputed then
                          DA.filter (tag /= _) model.tagsInputed
                    else if DA.length model.tagsInputed < maxTags then
                          DA.snoc model.tagsInputed tag
                    else
                          model.tagsInputed
            } /\ []
      where
      maxTags
            | DA.elem MoreTags model.user.privileges = maxFinalTags
            | otherwise = maxStartingTags

setLanguage ∷ String → ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setLanguage value model =
      model
            { languagesInputed = case DI.fromString value of
                    Nothing → []
                    Just id →
                          if DA.elem id model.languagesInputed then
                                DA.filter (id /= _) model.languagesInputed
                          else if DA.length model.languagesInputed < maxLanguages then
                                DA.snoc model.languagesInputed id
                          else
                                model.languagesInputed
            } /\ []

setAutoGenerated ∷ ProfileModel → What → String → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setAutoGenerated model what raw =
      case what of
            Name → model { updateRequestStatus = Nothing, nameInputed = value, generated = markGenerated, loading = value == Nothing } /\ [ generated (Proxy ∷ Proxy "nameInputed") ]
            Headline → model { updateRequestStatus = Nothing, headlineInputed = value, generated = markGenerated, loading = value == Nothing } /\ [ generated (Proxy ∷ Proxy "headlineInputed") ]
            Description → model { updateRequestStatus = Nothing, descriptionInputed = value, generated = markGenerated, loading = value == Nothing } /\ [ generated (Proxy ∷ Proxy "descriptionInputed") ]
      where
      value
            | DS.null raw = Nothing
            | otherwise = Just raw

      markGenerated = let g = DA.filter (what == _) model.generated in if value == Nothing then what : g else g

      generated ∷ ∀ r u. IsSymbol r ⇒ Cons r (Maybe String) u PM ⇒ Proxy r → _
      generated field = case value of
            Nothing → do
                  result ← routes.profile.generated { body: { field: what } }
                  case result of
                        Right (Response { body }) → do
                              pure <<< Just $ SetPField (R.set field (Just body))
                        Left err → do
                              pure <<< Just <<< SetPField $ _ { updateRequestStatus = Just <<< Failure $ SN.errorMessage err }
            _ → pure Nothing

setVisibility ∷ ProfileModel → ScreenModal → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setVisibility model modal = model { mode = Edit, visible = modal == ShowProfile } /\ []

setField ∷ ProfileModel → (ProfileModel → ProfileModel) → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
setField model upd = upd model { loading = false } /\ []

selectAvatar ∷ ProfileModel → ProfileModel /\ Array (Aff (Maybe ProfileMessage))
selectAvatar model = model /\ [ selectIt ]
      where
      selectIt = liftEffect do
            input ← getFileInput
            CCF.triggerFileSelect input
            pure Nothing